= System User Manual
:toc:
:toclevels: 1

include::partial$overview.adoc[]

== Installation

[IMPORTANT] 
.Pre-requisite
====
* Thales Digital Factory gitlab access or all the binaries of `sysbot`
* https://www.python.org/downloads/[Python 3.x]
* https://git-scm.com/downloads/guis[Git client]
* Python IDE ( ex: https://code.visualstudio.com/[Visual Studio Code])
====

=== Configure pip

[source, ini]
.`~/pip/pip.ini`
----
[global]
index-url = https://<email>:<token>@artifactory.sf-prod.ahe.tdp.infra.thales/artifactory/api/pypi/pypi/simple <1>
extra-index-url = https://__token__:<token>@gitlab.sf-prod.ahe.tdp.infra.thales/api/v4/groups/6556/-/packages/pypi/simple <2>
trusted-host = <3>
    artifactory.sf-prod.ahe.tdp.infra.thales
    gitlab.sf-prod.ahe.tdp.infra.thales
----
<1> Configure the primary PyPi repository (replace <username>:<token> with valid credentials)
<2> Configure the TDP gitlab PyPi repository (replace <token> with valid gitlab token)
<3> Configure the trusted host to disable SSL check

=== Client

[source,shell]
----
python -m venv .venv <1>
.\venv\Scripts\activate.bat <2>
pip install sysbot sysbotDrivers <3>
----
<1> Create a new python environment
<2> Enter in the python environment
<3> Install all the sysbot library

=== WebServer

[source,shell]
----
python -m venv .venv <1>
.\venv\Scripts\activate.bat <2>
pip install sysbot <3>
sysbot server run <4>
----
<1> Create a new python environment
<2> Enter in the python environment
<3> Install all the sysbot library
<4> Run the web server

By default, the bdd is a sqlite located at `~/.sysbot/sysbot.sqlite`

== Campaign execution 

[source,shell]
----
git clone https://gitlab.sf-prod.ahe.tdp.infra.thales/ndcn/ivq_drawer/tasq/campaign/ndcn.git <1>
cd ndcn <2>
robot -V vars/platforms/PF-A.yml functional
----
<1> Clone the NDCN robot framework test suites
<2> Move to the clone repository
<3> Load the PF-A variables and execute all test suite in functionnal repository


[NOTE] 
====
3 files are created :

* log.html: Details of the execution test step by step
* output.xml: All campaing information in a xml result files
* report.html: Overview of the campaign result
====

== Test suite redaction

Writing a test suite involves defining test cases that validate the functionality of your system. These test cases should be written in a way that they can be executed automatically, providing a reliable and repeatable way to verify your system. Here's a step-by-step guide on how to write a test suite.

=== Define the Settings 

At the beginning of your test suite, define the settings that will be used. This includes the name of the test suite, any tags that should be associated with it, and the documentation that describes what the test suite does. For example:

[source,robot]
----
*** Settings ***
Name              ILO <1>
Test Tags         MVP    BMC <2>
Documentation     Test suite for NDCN BMC component <3>

Library      Collections <4>
Library      sysbot.Helper <5>
Library      sysbot.dataloaders.DataloaderHandler <6>
Library      sysbotDrivers.os.windows.Adds <7>

Suite Teardown    Close all windows session <8>
----
<1> The name of the test suite
<2> Give tags to the test suite to organise test suite
<3> Short description of the test suite
<4> Import RobotFramwork library used to manipulate table
<5> Import Helper sysbot library that provide common keyword
<6> Import DataloaderHanlder to load some data
<7> Import all the keyword for Active Directory Domain Services
<8> Configure the teardown to close all windows connexion at the end of the test suite

=== Write Test Cases

After that, you should write your test cases. Each test case should have a clear name and consist of a series of steps that are executed in order. These steps can include actions like opening a session, executing a command, and verifying the result. 

[source,robot]
----
*** Test Cases *** <1>
Login on each server with the authentication provided in the user env <2>
        Open Windows Session    ${host_info}[hostname]    ${host_info}[ipv4]    admin_bmc    ${host_info}[password_bmc] <3>
----
<1> Test cases are delimited by the `*** Test Cases ***` balise.
<2> Name of the test case
<3> Use Open Windows Session keyword to open a session to the host

=== External source

* https://robotframework.org/robotframework/latest/RobotFrameworkUserGuide.html[RobotFramework User Manual]
* https://robotframework.org/robotframework/latest/libraries/BuiltIn.html[RobotFramework BuiltIn Keywords]
* https://robotframework.org/robotframework/latest/libraries/Collections.html[RobotFramework Collections Keywords]

=== Exemple of a test suite

[source,robot]
----
*** Settings ***
Name              HPE
Test Tags         MVP    BMC
Documentation     Test suite for testing the HPE BMC component of the NDCN system.

Library        Collections
Library        sysbot.Helper
Library        sysbot.dataloaders.DataloaderHandler
Library        sysbotDrivers.hardware.bmc.hpe.Dl360    AS    Bmc
Library        sysbotDrivers.hardware.network.${network.vendor.lower()}.${network.model.capitalize()}    AS    Network

Test Setup        Ignore hardware
Suite Teardown    Bmc.Close All Sessions


*** Variables ***
${BASELINE_DIR}=    vars/baselines

*** Keywords ***
Ignore hardware
    Skip If    '${bmc.model.lower()}' != 'dl360'    Out of Scope

*** Test Cases ***
Setup environment
    [Documentation]    Prepares the environment for testing components. 
    ...                - Loads baseline configuration for the specified box type.
    ...                - Initializes the user environment with token and host values.
    
    ${baseline}=    Import data from    yaml    file=${BASELINE_DIR}/${box_type}.yaml
    Set Suite Variable    ${baseline}

    ${user_env}=    Import data from    ndcn_user_env    token=${token}    host=${host}
    Set Suite Variable    ${user_env}

Login on each server with the authentication provided in the user env
    ${hosts}=    Get From Dictionary    ${user_env}[services][bmc]    hosts
    FOR    ${host}    IN    @{hosts.keys()}
        ${host_info}=    Get From Dictionary    ${hosts}    ${host}
        Bmc.Open Session   ${host_info}[hostname]    http    redfish    ${host_info}[ipv4]    443    admin_bmc    ${host_info}[password_bmc]
    END

The certificate is delivered by the PKI and it's valid
    ${hosts}=    Get From Dictionary    ${user_env}[services][bmc]    hosts
    FOR    ${host}    IN    @{hosts.keys()}
        ${host_info}=    Get From Dictionary    ${hosts}    ${host}
        ${result}=    Bmc.Get Certificate Issuer    ${host_info}[ipv4]    443
        Should be equal     ${result}    NDCN-RootCA
    END

The NDCN tag version is the same as the delivery
    ${hosts}=    Get From Dictionary    ${user_env}[services][bmc]    hosts
    FOR    ${host}    IN    @{hosts.keys()}
        ${host_info}=    Get From Dictionary    ${hosts}    ${host}
        ${result}=    Bmc.Get Ndcn Version    ${host_info}[hostname]
        Should be equal    ${result}    ${baseline}[common][ndcn_version]
    END

The datetime is sync with the ToR
    Network.Open Session    vip    ${network.protocol}    ${network.product}    ${user_env}[services][network][networks][bmc][local_ip_tor]    ${network.port}    admin_tor    ${user_env}[services][network][credentials][password_ssh]
    
    ${hosts}=    Get From Dictionary    ${user_env}[services][bmc]    hosts
    FOR    ${host}    IN    @{hosts.keys()}
        ${host_info}=    Get From Dictionary    ${hosts}    ${host}
        ${bmc_datetime}=    Bmc.Get UTC Datetime    ${host_info}[hostname]
        ${tor_datetime}=    Network.Get UTC Datetime   vip
        Should be equal    ${bmc_datetime}    ${tor_datetime}
    END
    [Teardown]    Network.Close All Sessions

The NTP source is the ToR
    ${hosts}=    Get From Dictionary    ${user_env}[services][bmc]    hosts
    FOR    ${host}    IN    @{hosts.keys()}
        ${host_info}=    Get From Dictionary    ${hosts}    ${host}
        ${result}=    Bmc.Get Ntp    ${host_info}[hostname]
        Should be equal    ${result}[0]    ${user_env}[services][network][networks][loopback][local_ip_tor]
    END
----